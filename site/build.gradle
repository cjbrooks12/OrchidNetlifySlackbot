// Buildscript dependencies and plugins
//----------------------------------------------------------------------------------------------------------------------
plugins {
    id "com.eden.orchidPlugin" version "0.15.3"
    id "org.jetbrains.kotlin.jvm" version "1.3.11"
}

// Kotlin Setup
//----------------------------------------------------------------------------------------------------------------------
sourceCompatibility = JavaVersion.VERSION_1_8
compileOrchidKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// Orchid setup
//----------------------------------------------------------------------------------------------------------------------
repositories {
    jcenter()
}

dependencies {
    def orchid_version = '0.15.3'

    orchidCompile "io.github.javaeden.orchid:OrchidCore:${orchid_version}"
    orchidRuntime "io.github.javaeden.orchid:OrchidCore:${orchid_version}"
    orchidRuntime "io.github.javaeden.orchid:OrchidPages:${orchid_version}"
    orchidRuntime "io.github.javaeden.orchid:OrchidEditorial:${orchid_version}"
    orchidRuntime "io.github.javaeden.orchid:OrchidSearch:${orchid_version}"
    orchidRuntime "io.github.javaeden.orchid:OrchidSwagger:${orchid_version}"
    orchidRuntime "io.github.javaeden.orchid:OrchidPluginDocs:${orchid_version}"
    orchidRuntime "io.github.javaeden.orchid:OrchidForms:${orchid_version}"
}

project.version = '1'

orchid {
    version = "${project.version}"
    theme = "Editorial"

    if (project.hasProperty('env') && project.property('env') == 'prod') {
        environment = 'production'
        String isPullRequest = System.getenv('PULL_REQUEST')
        if(isPullRequest == "true") {
            baseUrl = System.getenv('DEPLOY_URL')
        }
        else if(System.getenv('URL')) {
            baseUrl = System.getenv('URL')
        }
        else {
            baseUrl = 'http://localhost:8080'
        }
    }
    else {
        environment = 'debug'
        baseUrl = 'http://localhost:8080'
    }
}

tasks.build.dependsOn orchidBuild